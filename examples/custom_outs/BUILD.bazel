"""Demonstrate that js_outs can go to the desired file extensions."""

load("@aspect_bazel_lib//lib:testing.bzl", "assert_outputs")
load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_files")
load("@aspect_rules_swc//swc:defs.bzl", "swc_compile")
load("@bazel_skylib//rules:write_file.bzl", "write_file")

###############################
# Case 1: js extension variants
# .mts -> .mjs
swc_compile(
    name = "esmodule",
    srcs = ["b.mts"],
    source_maps = "true",
)

assert_outputs(
    name = "check_esmodule",
    actual = "esmodule",
    expected = [
        "examples/custom_outs/b.mjs",
        "examples/custom_outs/b.mjs.map",
    ],
)

###############################
# Case 2: user-defined js output paths
# Note, --config is documented on swcx CLI but isn't functional. See
# https://github.com/swc-project/swc/issues/4017#issuecomment-1374141572
# In the call to swc_compile below, we ought to be able to use
#   `args = ["--config", "module.format=" + format]`
# As a workaround, write our config to an rc file. Can be changed back when swcx supports --config.
[
    write_file(
        name = "_write_swcrc_" + format,
        out = "swcrc_" + format,
        content = [json.encode({
            "jsc": {
                "parser": {
                    "syntax": "typescript",
                },
            },
            "module": {
                "type": format,
            },
        })],
    )
    for format in [
        "commonjs",
        "es6",
    ]
]

[
    swc_compile(
        name = "compile_" + format,
        srcs = ["a.ts"],
        # The extension of the outputs can be modified using js_outs
        js_outs = [format + "/a." + ("cjs" if format == "commonjs" else "js")],
        out_dir = format,
        swcrc = "swcrc_" + format,
    )
    for format in [
        "commonjs",
        "es6",
    ]
]

write_source_files(
    name = "tests",
    files = {
        "expected.cjs": ":commonjs/a.cjs",
        "expected.js": ":es6/a.js",
    },
)
