load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@aspect_rules_js//js:defs.bzl", "js_binary", "link_js_package")
load("@swc_cli//:defs.bzl", "link_js_packages", "package")
load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_files")

write_source_files(
    name = "write_cli_repos",
    files = {
        "cli_repositories.bzl": "@swc_cli//:repositories.bzl",
    },
)

link_js_package(
    name = "link_core",
    src = "@npm__at_swc_core__1.2.185//:jsp",
)

link_js_packages()

# For stardoc to reference the files
exports_files(["swc.bzl"])

# This is the target rule authors should put in their "toolchains"
# attribute in order to get a runtime for the correct platform.
# See https://docs.bazel.build/versions/main/toolchains.html#writing-rules-that-use-toolchains
toolchain_type(
    name = "toolchain_type",
    visibility = ["//visibility:public"],
)

bzl_library(
    name = "repositories",
    srcs = ["repositories.bzl"],
    visibility = ["//visibility:public"],
    deps = [
        "//swc/private:toolchains_repo",
        "//swc/private:versions",
        "@bazel_tools//tools/build_defs/repo:http.bzl",
        "@bazel_tools//tools/build_defs/repo:utils.bzl",
    ],
)

# Workaround: js_binary doesn't support entry points under external yet
write_file(
    name = "gen_main",
    out = "main.cjs",
    content = ["require('@swc/cli')"],
)

js_binary(
    name = "cli",
    data = [
        # Don't use the shorter syntax here because the user or bzlmod
        # might use repository mapping to give a different name
        # to the repository, e.g.
        # @aspect_rules_swc.0.2.0.swc.npm__swc_core-1.2.119
        package("@swc/cli"),
        ":link_core",
    ],
    entry_point = "main.cjs",
    visibility = ["//visibility:public"],
)

bzl_library(
    name = "dependencies",
    srcs = ["dependencies.bzl"],
    visibility = ["//visibility:public"],
    deps = [
        "@bazel_tools//tools/build_defs/repo:http.bzl",
        "@bazel_tools//tools/build_defs/repo:utils.bzl",
    ],
)

bzl_library(
    name = "swc",
    srcs = ["swc.bzl"],
    visibility = ["//visibility:public"],
    deps = [
        "//swc/private:swc",
        "@bazel_skylib//lib:paths",
        "@bazel_skylib//lib:types",
    ],
)

bzl_library(
    name = "toolchain",
    srcs = ["toolchain.bzl"],
    visibility = ["//visibility:public"],
)
